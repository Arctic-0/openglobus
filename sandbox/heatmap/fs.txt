precision mediump float;

#define MAX_POINTS 30

uniform vec2 resolution;
// a point is: a position (x, y), a distance (z), a weight (w)
uniform vec4 points[MAX_POINTS];
uniform int pointsLength;


vec3 heat (float v) {
    float value = 1.-v;
    return (0.5 + 0.5 * smoothstep(0.0, 0.1, value)) * vec3(
        smoothstep(0.5, 0.3, value),
        value < 0.3 ? smoothstep(0.0, 0.3, value) : smoothstep(1.0, 0.6, value),
        smoothstep(0.4, 0.6, value)
    );
}

void main () { 
    vec2 p = gl_FragCoord.xy / resolution.xy;
    float sum = 0.0;
    for (int i = 0; i < MAX_POINTS; ++i) {
        if (i == pointsLength) break;
        vec4 point = points[i];
        sum += point.w * smoothstep(point.z, 0.0, distance(point.xy, p));
    }

    gl_FragColor = vec4(heat(sum), 1.0);
}